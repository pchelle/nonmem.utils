;Model Desc: NO MODEL DESCRIPTION
;Project Name: evaluations
;Project ID: NO PROJECT DESCRIPTION


$PROB PDFIX-2C-112-FFM

$INPUT ID OCC TIME TAD AMT RATE DV MDV EVID AGE BW HT FFM DRUGID DOSEKG BASELINE PREDOSE BLQ LLOQ IID SOURCEID INHIB DRUG=DROP SOURCE=DROP

$DATA "FINAL_DATASET.csv" IGNORE=@

$SUBROUTINE ADVAN3 TRANS4
 
$PK
TSOURCE=0.0
IF(SOURCEID==2) TSOURCE=THETA(9)
IF(SOURCEID==3) TSOURCE=THETA(10)
MU_1=LOG(THETA(1))+THETA(5)*LOG(FFM/53.0)+TSOURCE
MU_2=LOG(THETA(2))+THETA(6)*LOG(FFM/53.0)
MU_3=LOG(THETA(3))+THETA(7)*LOG(FFM/53.0)
MU_4=LOG(THETA(4))+THETA(8)*LOG(FFM/53.0)

BOV = ETA(3)
IF(OCC==2) BOV=ETA(4)
IF(OCC==3) BOV=ETA(5)
IF(OCC==4) BOV=ETA(6)
IF(OCC>=5) BOV=ETA(7)

CL = EXP(MU_1+ETA(1)+BOV) ; clearance
V1 = EXP(MU_2+ETA(2)) ; central volume 
Q  = EXP(MU_3) ; intercompartmental clearance
V2 = EXP(MU_4) ; peripheral volume  
 
PREDOSE2=PREDOSE
IF(PREDOSE2<=0.0) PREDOSE2=LLOQ/2.0
BASELINE2=BASELINE
CONST=MAX(BASELINE2,0.0)
IF(BASELINE2==-1.0) CONST=MIN(LLOQ/2.0,PREDOSE2)
TROUGH=MAX(PREDOSE2-CONST,0.0)
TTROUGH=0.0
 
;FOR THESE DATA SETS TIME WAS ADJUSTED SO THAT 0 WAS TIME OF PREDOSE VALUE.  SO, TTROUGH=0.0
; HOWEVER THIS IS NOT ALWAYS THE CASE, SO BE AWARE! 
S1 = V1
S2 = V2 
 
K=CL/V1
K12=Q/V1
K21=Q/V2
MRT=1/K
SUMK=K+K12+K21
SQ=DSQRT(SUMK*SUMK-4.0*K*K21)
ALPHA=(SUMK+SQ)/2.0D+00
BETA=(SUMK-SQ)/2.0D+00
TALPHA=LOG(2.0)/ALPHA
TBETA=LOG(2.0)/BETA
THALF=TBETA

$ERROR (OBS ONLY)
; NOTE THAT FOR 1 CMPT MODEL, ALPHA SHOULD BE USED, AND FOR 3 CMPT MODEL, GAMMA SHOULD BE USED
; use this if BLQs are used
IPRED = F+CONST+TROUGH*EXP(-BETA*(TIME-TTROUGH))
IRES=DV-IPRED
SD=DSQRT(SIGMA(1,1)*IPRED*IPRED+SIGMA(2,2))
IWRES=IRES/SD
IF(BLQ<=0.0) THEN
F_FLAG=0
Y=IPRED+IPRED*EPS(1)+EPS(2)
ELSE
F_FLAG=1
DUM = (BLQ - IPRED) / SD
CUMD = PHI(DUM)+1.0E-30
Y = CUMD
MDVRES=1
ENDIF
  
$THETA
(0, 0.25) ; [CL]
(0, 6.2) ; [V1]
(0, 0.18) ; [Q]
(0, 2.9) ; [V2]
0.62 ; [FFM on CL]
0.82 ; [FFM on V1]
0.49 ; [FFM on Q]
0.91 ; [FFM on V2]
0.19 ; [Uppsala on CL]
-0.37 ; [Industry on CL]

$OMEGA BLOCK(2) 
0.12 ; [P]
0.01 0.078 ; [P]
$OMEGA BLOCK(1) 0.033 ; [P] occ=1
$OMEGA BLOCK(1) SAME ; [P] occ=2 
$OMEGA BLOCK(1) SAME ; [P] occ=3 
$OMEGA BLOCK(1) SAME ; [P] occ=4 
$OMEGA BLOCK(1) SAME ; [P] occ>=5

$SIGMA 
0.020 ; [P]
0.0 FIX ; [A]

$ESTIMATION METH=COND LAPLACE INTERACTION MAXEVAL=9999 PRINT=1 NOHABORT 
MSFO=F9S_PLASMADERIVED_2CO_200_V3_RPOP.msf

$COV UNCONDITIONAL

$TABLE ID TIME TAD OCC DV MDV EVID PRED IPRED CWRES IWRES NPDE BLQ 
    ETAS(1:LAST) BOV CL V1 Q V2 THALF BW AGE HT FFM DRUGID DOSEKG BASELINE INHIB SOURCEID 
    ONEHEADER NOPRINT FILE=F9S_PLASMADERIVED_2CO_200_V3_RPOP.tab

